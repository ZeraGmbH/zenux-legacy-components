file(GLOB SOURCES 
    ${CMAKE_CURRENT_SOURCE_DIR}/*.cpp
    )

file(GLOB HEADER 
    ${CMAKE_CURRENT_SOURCE_DIR}/*.h
    )

add_library(zera-math SHARED
    ${SOURCES}
    ${HEADER}
    )

target_link_libraries(zera-math
    PUBLIC
    ${QTX}::Core
    # ask linker to help us finding unresolved symbols
    "-Wl,--no-undefined"
    )

target_include_directories(zera-math
    PUBLIC
    $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}/zenux-math>
    $<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}>
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
    )

set_target_properties(zera-math PROPERTIES VERSION ${PROJECT_VERSION})
set_target_properties(zera-math PROPERTIES SOVERSION ${PROJECT_VERSION_MAJOR})

generate_export_header(zera-math)
install(FILES
    ${CMAKE_CURRENT_BINARY_DIR}/zera-math_export.h
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/zenux-math
    )

export(TARGETS zera-math NAMESPACE Zera:: FILE ZenuxLegacyComponentsTargets.cmake)
install(TARGETS zera-math
    EXPORT ZenuxLegacyComponentsExport
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
    )

install(
    FILES ${HEADER}
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/zenux-math
    )
